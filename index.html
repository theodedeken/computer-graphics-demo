<!DOCTYPE html>
<html>

<script id="vertex-shader" type="x-shader/x-vertex">
varying vec3 L; /* light vector in texture-space coordinates */

attribute vec2 vTexCoord;
attribute vec4 vPosition;

uniform vec4 normal;
uniform vec4 lightPosition;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 normalMatrix;
uniform vec3 objTangent; /* tangent vector in object coordinates */

varying vec2 fTexCoord;

void main()
{
    gl_Position = projectionMatrix*modelViewMatrix*vPosition;

    fTexCoord = vTexCoord;

    vec3 eyePosition = (modelViewMatrix*vPosition).xyz;
    vec3 eyeLightPos = (modelViewMatrix*lightPosition).xyz;

    /* normal, tangent and binormal in eye coordinates */

    vec3 N = normalize(normalMatrix*normal.xyz);
    vec3 T  = normalize(normalMatrix*objTangent);
    vec3 B = cross(N, T);

    /* light vector in texture space */

    L.x = dot(T, eyeLightPos-eyePosition);
    L.y = dot(B, eyeLightPos-eyePosition);
    L.z = dot(N, eyeLightPos-eyePosition);  
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">

precision mediump float;

varying vec3 L;
varying vec2 fTexCoord;

uniform sampler2D normalMap;
uniform sampler2D texture;

uniform bool apply_bump;
uniform bool apply_texture;

void main()
{  
    vec4 diffuse;
    if (apply_texture) {
        diffuse = texture2D(texture, fTexCoord);
    }
    else {
        diffuse = vec4(0.75, 0.75, 0.75, 1.0);
    }

    vec4 N = texture2D(normalMap, fTexCoord);
    float Kd;
    if (apply_bump) {
        vec3 NN =  normalize(2.0*N.xyz-1.0);
        vec3 LL = normalize(L);
        Kd = max(dot(NN, LL), 0.0);
    }
    else {
        Kd = 1.0;
    }

    gl_FragColor = vec4(Kd*diffuse.xyz, 1.0);
}
</script>

<script type="text/javascript" src="common/webgl-utils.js"></script>
<script type="text/javascript" src="common/InitShaders.js"></script>
<script type="text/javascript" src="common/MV.js"></script>
<script type="text/javascript" src="bump.js"></script>

<body>
<input type="checkbox" id="bump-switch" onclick="apply_settings();" checked> Toggle bump mapping <input type="checkbox" id="texture-switch" onclick="apply_settings();" checked> Toggle texture

<canvas id="gl-canvas" width="768" height="768">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
</body>
</html>
